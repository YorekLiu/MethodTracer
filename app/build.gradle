plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
if (applyPlugin.toBoolean()) {
    apply plugin: 'method-trace'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdk

    defaultConfig {
        applicationId "xyz.yorek.plugin.sample"
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    if (applyPlugin.toBoolean()) {
        implementation "com.github.YorekLiu.MethodTracer:annotation:$remotePluginVersion"
    } else {
        implementation project(":annotation")
    }
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}

if (applyPlugin.toBoolean()) {
    methodTrace {
        enable = true

        /**
         * 扫描结果文件输出路径，默认为build/outputs/report/report_{variantName}_{ts}.txt
         */
        // output = null

        /**
         * 要检测的API表，整体是一个Map<String, List<String>>，格式为类名:List<方法名>
         * 其中类名支持+
         * 若方法名为*，则为检测该类下所有方法的调用
         */
        apiList = [
                "java.lang.Thread"                  : ["<init>", "start"],
                "android.telephony.TelephonyManager": ["getDeviceId"],
                "android.content.pm.PackageManager" : ["getInstalledPackages"],
                "android.widget.ImageView+"         : ["setImageDrawable", "setImageBitmap"],
                "android.app.ActivityManager"       : ["getRunningAppProcesses"],
                "android.os.Process"                : ["myPid"]
        ]
    }
}